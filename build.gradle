/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

buildscript {
    ext.dependencies = [
        kotlin: '1.2.51',
        coroutines: '0.23.4',
        sentry: '1.7.3',
        supportLibraries: '27.1.1',
        constraintLayout: '1.1.0',
        // Testing
        junit: '4.12',
        // Mozilla
        mozillaComponents: '0.17'
    ]

    // Synchronized versions numbers of GeckoView (Nightly) artifacts.
    ext.geckoNightly = [
        // Discover nightly builds: https://tools.taskcluster.net/index/gecko.v2.mozilla-central.nightly
        nightlyDate: '2018.07.24',
        revision: '1e5fa52a612e8985e12212d1950a732954e00e45',
        version: '63.0.20180724100046'
    ]

    // Synchronized versions numbers of GeckoView (Beta) artifacts.
    ext.geckoBeta = [
        // (62.0b13)
        revision: 'dd92dec96711e60a8c6a49ebe584fa23a453a292',
        version: '62.0.20180730180407'
    ]

    ext.geckoRelease = [
        // 61.0
        revision: '785d242a5b01d5f1094882aa2144d8e5e2791e06',
        version: '61.0.20180621125625'
    ]

    ext.build = [
        compileSdkVersion: 27,
        targetSdkVersion: 27,
        minSdkVersion: 21
    ]

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${project.ext.dependencies['kotlin']}"
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.0.0.RC6-4")
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// -------------------------------------------------------------------------------------------------
// -- detekt ---------------------------------------------------------------------------------------
// -------------------------------------------------------------------------------------------------

detekt {
    version = "1.0.0.RC6-4"
    profile("main") {
        input = "$projectDir"
        config = "$projectDir/config/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        output = "$projectDir/build/reports/detekt"
    }
}

// -------------------------------------------------------------------------------------------------
// -- ktlint ---------------------------------------------------------------------------------------
// -------------------------------------------------------------------------------------------------

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.20.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "components/**/*.kt"
}
