/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

buildscript {
    ext.dependencies = [
        kotlin: '1.2.40',
        coroutines: '0.22.5',
        sentry: '1.7.3',
        supportLibraries: '27.1.1',
        constraintLayout: '1.1.0',
        // Testing
        junit: '4.12',
        // Mozilla
        mozillaComponents: '0.11'
    ]

    // Synchronized versions numbers of GeckoView (Nightly) artifacts.
    ext.geckoNightly = [
            // Discover nightly builds: https://tools.taskcluster.net/index/gecko.v2.mozilla-central.nightly
            nightlyDate: '2018.06.21',
            revision: 'e834d23a292972ab4250a8be00e6740c43e41db2',
            version: '62.0.20180621100051'
    ]

    // Synchronized versions numbers of GeckoView (Beta) artifacts.
    ext.geckoBeta = [
            // (61.0b14)
            revision: '585281dde9604ddd197693ca5330f2b95987a181',
            version: '61.0.20180614135649'
    ]

    ext.geckoRelease = [
            revision: 'a0b222c551f586904f51228c49149d9b6b7e2a81',
            version: '60.0.2'
    ]

    ext.build = [
        compileSdkVersion: 27,
        targetSdkVersion: 27,
        minSdkVersion: 21
    ]

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${project.ext.dependencies['kotlin']}"
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.0.0.RC6-4")
}

allprojects {
    repositories {
        google()
        jcenter()

        // Temporary until all modules are synced to JCenter
        maven {
            url "https://dl.bintray.com/pocmo/Mozilla-Mobile"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// -------------------------------------------------------------------------------------------------
// -- detekt ---------------------------------------------------------------------------------------
// -------------------------------------------------------------------------------------------------

detekt {
    version = "1.0.0.RC6-4"
    profile("main") {
        input = "$projectDir"
        config = "$projectDir/config/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        output = "$projectDir/build/reports/detekt"
    }
}

// -------------------------------------------------------------------------------------------------
// -- ktlint ---------------------------------------------------------------------------------------
// -------------------------------------------------------------------------------------------------

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.20.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "components/**/*.kt"
}
