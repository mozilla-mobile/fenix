// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.11'
    ext.mozilla_components_version = '0.34.2'
    ext.detekt_version = '1.0.0-RC12'
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    // The version number is duplicated in the detekt block. We are unable to use constants due to
    // limitations of the gradle plugin. We've decided to stick with duplication for now since the
    // other methods involve using undocumented APIs
    // https://docs.gradle.org/current/userguide/plugins.html#plugins_dsl_limitations
    id "com.github.spotbugs" version "1.6.6"
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC12"
}

allprojects {
    repositories {
        google()
        maven {
            url "https://maven.mozilla.org/maven2"
        }
        jcenter()
    }
}

detekt {
    // The version number is duplicated, please refer to plugins block for more details
    toolVersion = "$detekt_version"
    config = files("$projectDir/quality/detekt.yml")
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    reports {
        xml {
            enabled = false
        }
        html {
            enabled = true
            destination = file("$projectDir/app/build/reports/detekt.html")
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.29.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "**/*.kt"
}
